apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

ext {

    libraryVersionCode = 2
    libraryVersion = "0.2.0"

    bintrayRepo = 'Common'
    bintrayName = 'SharedPreferences'

    publishedGroupId = 'com.daydreamapplications'
    libraryName = 'SharedPreferences'
    artifact = 'shared-preferences'

    libraryDescription = 'Tools for use of SharedPreferences using Kotlin delegates.'

    siteUrl = 'https://github.com/daydreamapps/shared-preferences'
    gitUrl = 'https://github.com/daydreamapps/shared-preferences.git'

    developerId = 'spatmore'
    developerName = 'Samuel Patmore'
    developerEmail = 'daydreamapplications@googlemail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

tasks.withType(Javadoc).all { // Required to prevent javadoc failing on Kotlin files
    enabled = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = getLocalProperty("bintray.user")
    key = getLocalProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}

def getLocalProperty(name) {
    try {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        return properties.getProperty(name)
    } catch (Exception e) {
        println("Exception: ${e}")
        return ""
    }
}